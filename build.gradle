//file:noinspection GroovyAssignabilityCheck
plugins {
  id "org.quiltmc.loom" version "0.12.+"
  id "org.quiltmc.gradle.licenser" version "1.+"
  id "com.modrinth.minotaur" version "2.+"
  id "maven-publish"
}

archivesBaseName = "quilt_loading_screen"
version = "$loom.modVersion+$minecraft_version"

repositories {
  mavenLocal()
  maven { url "https://maven.terraformersmc.com" }
  maven { url "https://api.modrinth.com/maven" }
}

dependencies {
  minecraft "com.mojang:minecraft:$minecraft_version"
  mappings(loom.layered {addLayer quiltMappings.mappings(
    "org.quiltmc:quilt-mappings:$minecraft_version+build.$quilt_mappings:v2"
  )})
  modImplementation "org.quiltmc:quilt-loader:$loader_version"
  modImplementation "org.quiltmc:qsl:$qsl_version+$minecraft_version-SNAPSHOT"
  modImplementation include("maven.modrinth:midnightlib:$midnightlib_version")
  modImplementation("com.terraformersmc:modmenu:$modmenu_version") { transitive = false }

  // Fabric API modules required for dependencies
  ["lifecycle-events-v1", "renderer-registries-v1", "resource-loader-v0", "screen-api-v1"].forEach {
    modRuntimeOnly "org.quiltmc.quilted-fabric-api:fabric-$it:+"
  }
}

idea { module { downloadJavadoc = false } }

tasks.withType(JavaCompile).forEach {
  it.options.encoding = "UTF-8"
  it.options.release.set(17)
  it.sourceCompatibility = 17
}

jar { from("LICENSE", "README.md") }

license {
  rule "HEADER"
  include "**/*.java"
}

import com.modrinth.minotaur.dependencies.ModDependency
modrinth {
  projectId = "VPU6VYVP"
  uploadFile = remapJar
  dependencies = [
    new ModDependency("qvIfYCYJ", "required"),
    new ModDependency("P7dR8mSH", "incompatible"),
  ]
  changelog = """
**The first release that does not use Fabric! You must download [Quilt Standard Libraries](https://modrinth.com/mod/qsl) and [Quilt Loader](https://quiltmc.org).**
- 3.x series is now for 1.18.x instead of 1.19 like before. For 1.19, use 4.x.
- Add Oliver-does-code's Quilt UI as a disabled-by-default resource pack
- Update textures to be based off of emotes rather than server banner
- Use Quilt stuff instead of Fabric stuff
- Lots of internal refactors
"""
}

java { withSourcesJar() }

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId = 'coffee.waffle'
      artifactId = archivesBaseName
      version = version
      from components.java
    }
  }
  repositories {
    maven {
      url = System.getenv().MAVEN_URL
      credentials {
        username = System.getenv().MAVEN_USER
        password = System.getenv().MAVEN_PASS
      }
      authentication { basic(BasicAuthentication) }
    }
  }
}
